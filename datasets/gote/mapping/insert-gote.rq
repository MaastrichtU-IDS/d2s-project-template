PREFIX d2smodel: <https://w3id.org/d2s/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/d2s/vocab/>
INSERT
{
  GRAPH <?_output> {  
    ?protUri a bl:Protein ;
      bl:id ?protUriId . 

    ?tissueUri a bl:GrossAnatomicalStructure ; # tissue
      bl:id ?tissueUriId ;
      bl:name ?tissueName .

    ?pathwayUri a bl:Pathway ; # tissue-specific molecular pathway
      bl:id ?pathwayUriId ;
      bl:name ?pathwayName ;
      bl:part_of ?tissueUri .

    ?interactionUri a bl:MacromolecularMachineToBiologicalProcessAssociation ;
      bl:relation bl:expressed_in ;
      bl:subject ?protUri ;
      bl:object ?pathwayUri ;
      bl:p_value ?pathwayPvalue ;
      bl:quantifier_qualifier ?highScoreUri ;
      bl:quantifier_qualifier ?specificScoreUri .

    ?highScoreUri a bl:RelationshipQuantifier ;
      bl:id "zScoreHighGpcrExpression" ;
      bl:name "High GPCR z-score" ;
      bl:description "z-score of GPCR represents how high the GPCR is expressed in the tissue" ;
      bl:node_property ?zScoreHighGpcrExpression .

    ?specificScoreUri a bl:SpecificityQuantifier ;
      bl:id "zScoreSpecificExpression" ;
      bl:name "Specific GPCR z-score" ;
      bl:description "z-score of GPCR represents how specific the GPCR is expressed in the tissue" ;
      bl:node_property ?zScoreSpecificExpression .

 }
}
WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {
      # gpcr_pathway.R: predicting G-protein independent pathways transduced by beta-arrestins.
      # gpcr_gp_pathway.R: predicting G-protein dependent pathways transduced by G-proteins.
      # do a bind on contains _gp_ to have different type?

      ?s d2smodel:Gpcruniprotid ?protId .
      ?s d2smodel:Pathwaynames ?pathwayName .
      ?s d2smodel:Tissuename ?tissueName .

      BIND( iri(concat("https://identifiers.org/uniprot/", ?protId ) ) AS ?protUri )
      BIND(str(?protUri) AS ?protUriId)
      BIND( iri(concat("https://w3id.org/d2s/data/pathway/", md5(?pathwayName) ) ) AS ?pathwayUri )
      BIND(str(?pathwayUri) AS ?pathwayUriId)
      BIND( iri(concat("https://w3id.org/d2s/data/tissue/", md5(?tissueName) ) ) AS ?tissueUri )
      BIND(str(?tissueUri) AS ?tissueUriId)
      BIND( iri(concat("https://w3id.org/d2s/data/protein/interaction/", md5(concat(?protId, ?tissueName, ?pathwayName)) ) ) AS ?interactionUri )

       OPTIONAL { 
         ?s d2smodel:Pathwaypvalue ?pathwayPvalue .
         BIND( iri(concat(str(?interactionUri), "/pathway_pvalue")) AS ?pathwayPvalueUri)
       } OPTIONAL { 
         ?s d2smodel:Zscorehighgpcrexpression ?zScoreHighGpcrExpression .
         BIND( iri(concat(str(?interactionUri), "/high_score")) AS ?highScoreUri)
       } OPTIONAL { 
         ?s d2smodel:Zscorespecificexpression ?zScoreSpecificExpression .
         BIND( iri(concat(str(?interactionUri), "/specific_score")) AS ?specificScoreUri)
       }

    }
  }
}