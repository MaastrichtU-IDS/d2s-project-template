PREFIX d2sgeneric: <http://data2services/data/hgnc/hgnc_complete_set.tsv/>
PREFIX d2smodel: <http://data2services/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/data2services/vocab/>
PREFIX oban: <http://purl.org/oban/>
PREFIX wp: <http://vocabularies.wikipathways.org/wp#>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>

INSERT
{
  GRAPH <?_output> {  

    # A physical entity consisting of a sequence of amino acids; a protein monomer; a single polypeptide chain. 
    # Example: The epidermal growth factor receptor (EGFR) protein.

    ?uri a bl:Protein ;
          bl:id ?proteinUri ;
          bl:name ?displayName ;
          bl:description ?comment ;
          bl:has_evidence ?xrefPublication ;
          bl:has_evidence ?evidenceUri .
 }
}
WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      # WE NEED:
      # Complex, DNA, Protein, RNA, SmallMolecule,DnaRegion, RnaRegion
      # Gene

      ?proteinUri a bp:Protein ;
          bp:entityReference ?uri . # Use it has URI. usually ido (uniprot, refseq, chebi) or http://pathwaycommons.org/pc2/#SmallMoleculeReference_5db71a6750411a9300c2ec710b0de52b
          
          # bp:dataSource <http://pathwaycommons.org/pc2/#pid> .
          #bp:feature 84054: bad id
          #bp:availability 83477: a string with copyright info
          #bp:standardName 21367: another label
          #bp:memberPhysicalEntity: member of another protein. Use isPartOf? with entityReference

      OPTIONAL {
        ?proteinUri bp:comment ?comment .
      } OPTIONAL {
        ?proteinUri bp:displayName ?displayName .
      } OPTIONAL {
        ?proteinUri bp:evidence [ bp:xref ?evidenceUri ] .
        ?evidenceUri a bp:PublicationXref . # only Pubmed identifiers.org
      } OPTIONAL {
        ?proteinUri bp:xref ?xrefPublication .
        ?xrefPublication a bp:PublicationXref . # only Pubmed identifiers.org
      } OPTIONAL {
        ?proteinUri bp:cellularLocation ?cellularLocation ;
        # Bad URI. Use locates? http://pathwaycommons.org/pc2/#CellularLocationVocabulary_dc68fffeee0259e0d3bd7a3f6d0cc067
      }

      # NOT WORKING: FILTER(?uriType NOT IN ( <http://www.biopax.org/release/biopax-level3.owl#SmallMoleculeReference> , <http://www.biopax.org/release/biopax-level3.owl#RnaReference> , <http://www.biopax.org/release/biopax-level3.owl#ProteinReference> ))
      FILTER regex( str(?uri), "http:\\/\\/identifiers.org\\/" )
      # Don't take biopax references that don't have proper identifiers.org

    }
  }
}