PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>

INSERT
{
  GRAPH <?_output> {  

    # BiochemicalReaction
    # Reactome: https://graphdb.dumontierlab.com/resource?uri=http:%2F%2Fidentifiers.org%2Freactome%2FR-HSA-6785732
    # https://graphdb.dumontierlab.com/resource?uri=http:%2F%2Fidentifiers.org%2Freactome%2FR-HSA-109278
    # https://graphdb.dumontierlab.com/resource?uri=http:%2F%2Fpathwaycommons.org%2Fpc2%2F%23BiochemicalReaction_48c61a65e060f872b394636f8a8524eb
    
    ?reactionUri a bl:BiologicalProcess ; # Or bl:ThingToThingAssociation ?
          # http://purl.obolibrary.org/obo/NCBITaxon_9606 link using bl:AnatomicalEntityToAnatomicalEntityPartOfAssociation ?
          bl:name ?displayName ;
          bl:synonym ?synonym ;
          bl:description ?comment ;
          #bl:category ?category ; # BiochemicalReaction?
          #bl:located_in ?eCNumberUri ; # Or use bl:GenomicSequenceLocalization ?
          bl:has_input ?reactant ;
          #bl:object ?product ;
          #bl:relation bl:produces ; # should be 'is transformed into'? Directly use bl:produces atm
          bl:produces ?product ;
          bl:has_evidence ?xrefPublication ;
          bl:has_evidence ?evidenceUri ;
          bl:has_participant ?participant .
          #bl:publications ?pathwayReferences .
 }
}
WHERE {
  SERVICE <?_service>  {
    #GRAPH <?_input> { # We need to use implicit graph for inference
    #GRAPH <http://pathwaycommons.org/pc2/graph> {
      ?reactionUri a bp:BiochemicalReaction ;
        bp:dataSource <http://pathwaycommons.org/pc2/#reactome> .

      # interaction
      #   - conversion
      #     - BiochemicalReaction
      #   - control
      #     - catalysis
        
      OPTIONAL {
        ?reactionUri bp:participant ?participant . # other pathways or interactions
      } OPTIONAL {
        ?reactionUri bp:eCNumber ?eCNumber . # e.g.: 2.7.10 identifiers.org/ec-code/3.1.3.5
        #BIND(iri(concat("https://identifiers.org/ec-code/", ?eCNumber)) AS ?eCNumberUri)
      #} OPTIONAL {
      #  ?reactionUri bp:participantStoichiometry ?participantStoichiometry .
      } OPTIONAL {
        ?reactionUri bp:conversionDirection ?conversionDirection ; # LEFT_TO_RIGHT
          bp:right ?right ;
          bp:left ?left .
        # Can be bp:Protein, bp:SmallMolecule, bp:Complex, bp:DnaRegion, bp:Rna, bp:PhysicalEntity, bp:Dna, bp:RnaRegion
        BIND( if( ?conversionDirection = "RIGHT_TO_LEFT",
              ?right ,
              ?left )
         AS ?reactant )
        BIND( if( ?conversionDirection = "RIGHT_TO_LEFT",
              ?left ,
              ?right )
         AS ?product )
      #  ?reactionUri bp:left ?left . # conversion goes from the left participant to the right participant
      #  ?reactionUri bp:right ?right .
      } OPTIONAL {
        ?reactionUri bp:comment ?comment .
      } OPTIONAL {
        ?reactionUri bp:displayName ?displayName .
      } OPTIONAL {
        ?reactionUri bp:name ?synonym .
      } OPTIONAL {
        ?reactionUri bp:evidence [ bp:xref ?evidenceUri ] .
        ?evidenceUri a bp:PublicationXref . # only Pubmed identifiers.org
        # xref can be unificationXref (identifier resolution) or publicationXref or relationshipXref
      } OPTIONAL {
        ?reactionUri bp:xref ?xrefPublication .
        ?xrefPublication a bp:PublicationXref . # only Pubmed identifiers.org
      }
    #}
  }
}
