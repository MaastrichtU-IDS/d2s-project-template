PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX bp: <http://www.biopax.org/release/biopax-level3.owl#>

INSERT
{
  GRAPH <?_output> {  
    #?pathwayUri bl:precedes ?nextStepProcess .
    
    ?pathwayStep a bl:BiologicalProcess ; # TODO: Or bl:Association? Atm it is the same type as BiochemicalReaction...
      bl:description ?comment ;
      bl:has_participant ?stepProcess ; # e.g. BiochemicalReaction and Catalysis
      bl:precedes ?nextPathwayStep ;
      bl:part_of ?pathwayUri ;
      bl:has_evidence ?xrefPublication ;
      bl:has_evidence ?evidenceUri .
 }
}
WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {
    #GRAPH <http://pathwaycommons.org/pc2/graph> {

      ?pathwayUri a bp:Pathway ;
        bp:dataSource <http://pathwaycommons.org/pc2/#reactome> .
      ?pathwayUri bp:pathwayOrder ?pathwayStep . # Order of processes in pathways
      #?pathwayStep a bp:PathwayStep . # Or use this? But can't filter dataSource

      # All properties
      # bp:comment, bp:stepProcess, bp:nextStep, bp:stepConversion, bp:stepDirection, bp:evidence

      # A PathwayStep can have multiple stepProcess: bp:BiochemicalReaction and bo:Catalysis for example. And multiple nextSteps
      # https://graphdb.dumontierlab.com/resource?uri=http:%2F%2Fpathwaycommons.org%2Fpc2%2F%23PathwayStep_096ee5f002fa354c8c00e45e09e8547b
      # https://graphdb.dumontierlab.com/resource?uri=http:%2F%2Fpathwaycommons.org%2Fpc2%2F%23PathwayStep_ef1fb101b5a45d37c65550c3ea34dd86
      # In BioLink we need an entity that can combine an association/BiologicalProcess with a catalyser (which can be a BiochemicalReaction also) 
      # that can precedes multiple other steps. I can reuse the PathwayCommons pathwayStep URI
      # Just an bl:Association with catalysis reaction as relation that produces entities? Or something like "upregulate the production of"
      # Or a bl:BiologicalProcess ?

      # Can a PathwayStep be also a bl:Pathway but with a category? Or should it be an association? More like this, 
      # it should be an association included in a pathway in a certain order
      # just a bl:part_of in the BiologicalProcess (formerly PathwayStep) should be good?
      # And then we don't need to add information to point from the bl:Pathway to this pathwayStep
      # the bl:Pathway just holds information about has_participant

      OPTIONAL {
        ?pathwayStep bp:stepProcess ?stepProcess .
      } OPTIONAL {
        ?pathwayStep bp:nextStep ?nextPathwayStep .
      } OPTIONAL {
        # The central process that take place at this step of the biochemical pathway.
        ?pathwayStep bp:stepConversion ?stepConversion .
      } OPTIONAL {
        # LEFT_TO_RIGHT or RIGHT_TO_LEFT
        ?pathwayStep bp:stepDirection ?stepDirection .
      } OPTIONAL {
        ?pathwayStep bp:comment ?comment .
      } OPTIONAL {
        ?pathwayUri bp:evidence [ bp:xref ?evidenceUri ] .
        ?evidenceUri a bp:PublicationXref . # only Pubmed identifiers.org
        # xref can be unificationXref (identifier resolution) or publicationXref or relationshipXref
      }
      
      #?nextPathwayStep bp:stepProcess ?nextStepProcess .
    }
  }
}
