PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/d2s/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT {
  GRAPH <?_output> {

    # TODO: biolink, we should have a ChemicalToVariantAssociation
    # That could be a mixin with VariantToThingAssociation
    ?drugVariantAssociationUri a bl:ChemicalToThingAssociation ; 
      bl:relation bl:affects_activity_of ;
      bl:subject ?drugUri ;
      bl:object ?snpUri ;
      bl:description ?associationDescription ;
      bl:description ?adverseReactionDescription ;
      bl:description ?alleleDescription ;
      bl:publications ?pubmedUri .

    ?snpUri a bl:SequenceVariant ;
      bl:id ?snpCurie ;
      bl:name ?pathwayName ;
      bl:has_gene ?geneUri .
      # bl:has_biological_sequence (optional)
  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      # Get primary DrugBank ID of drug
      ?drugObj x2rm:child_drugbank-id [ 
        x2rm:attribute_primary "true" ; 
        x2rm:hasValue ?drugbankId 
      ] .
      BIND(iri(concat("https://identifiers.org/drugbank:", ?drugbankId)) AS ?drugUri)
      ?drugObj x2rm:child_snp-adverse-drug-reactions [ 
        x2rm:child_reaction ?snpObj
      ] .

      # Iterate on
      ?snpObj a x2rm:drugbank.drug.snp-adverse-drug-reactions.reaction ;

        x2rm:child_rs-id [ x2rm:hasValue ?snpRsId ] ;
        x2rm:child_gene-symbol [ x2rm:hasValue ?geneSymbol ] .
      
      BIND( iri(concat("https://identifiers.org/hgnc.symbol:", ?geneSymbol)) AS ?geneUri )

      OPTIONAL{
        ?snpObj x2rm:child_pubmed-id [ x2rm:hasValue ?pmid ]
        BIND( iri(concat("https://identifiers.org/pubmed:", ?pmid)) AS ?pubmedUri )
      }
      OPTIONAL{
        ?snpObj x2rm:child_allele [ x2rm:hasValue ?allele ]
        # TODO: resolve allele to an URI?
        BIND( concat("Allele: ", ?allele) AS ?alleleDescription )
      }
      OPTIONAL{
        ?snpObj x2rm:child_adverse-reaction [ x2rm:hasValue ?adverseReaction ]
        BIND( concat("Adverse reaction: ", ?adverseReaction) AS ?adverseReactionDescription )
      }
      OPTIONAL{ ?snpObj x2rm:child_description [ x2rm:hasValue ?associationDescription ] }

      BIND( concat("snpdb:", ?snpRsId) AS ?snpCurie )
      BIND( iri(concat("https://identifiers.org/", ?snpCurie)) AS ?snpUri )
      BIND( iri(concat("https://w3id.org/d2s/data/association/chemical_variant:", concat(?drugbankId, "_", ?snpRsId))) AS ?drugVariantAssociationUri )

      # Example:
      # <snp-adverse-drug-reactions>
      # <reaction>
      #   <protein-name>Dihydropyrimidine dehydrogenase [NADP(+)]</protein-name>
      #   <gene-symbol>DPYD</gene-symbol>
      #   <uniprot-id>Q12882</uniprot-id>
      #   <rs-id>rs3918290</rs-id>
      #   <allele>DPYD*2A</allele>
      #   <adverse-reaction>G &gt; A</adverse-reaction>
      #   <description>The presence of this genotype in DPYD is associated with an increased risk of drug-related toxicity from tegafur therapy.</description>
      #   <pubmed-id>23988873</pubmed-id>
      # </reaction>
    }
  }
}
