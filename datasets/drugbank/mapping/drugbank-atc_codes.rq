PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/d2s/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT {
  GRAPH <?_output> {

    # TODO: should we use bl:OntologyClass?
    ?atcCodeUri a bl:OntologyClass ;
      bl:id ?atcCodeCurie ;
      bl:name ?atcName .

    # TODO: avoid using category? See GitHub issue
    ?drugUri bl:category ?atcCodeUri .
  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      # Get primary DrugBank ID of drug
      ?drugObj x2rm:child_drugbank-id [ 
          x2rm:attribute_primary "true" ; 
          x2rm:hasValue ?drugbankId 
        ] ;
        x2rm:child_name [ x2rm:hasValue ?drugName ].
      BIND(iri(concat("https://identifiers.org/drugbank:", ?drugbankId)) AS ?drugUri)

      ?drugObj x2rm:child_atc-codes [ 
        x2rm:child_atc-code ?atcCodeObj
      ] .

      # Iterates on
      ?atcCodeObj a x2rm:drugbank.drug.atc-codes.atc-code ;
        x2rm:attribute_code ?atcCode .

      BIND( lcase(?drugName) AS ?atcName)
      BIND(concat("atc:", ?atcCode) AS ?atcCodeCurie)
      BIND(iri(concat("https://identifiers.org/", ?atcCodeCurie)) AS ?atcCodeUri)

      # We don't get all levels, it should be resolved by the ATC ontology

    }
  }
}
