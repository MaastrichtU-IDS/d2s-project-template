PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/d2s/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT {
  GRAPH <?_output> {

    # TODO: should we use https://biolink.github.io/biolink-model/docs/OrganismTaxon.html
    # Or https://biolink.github.io/biolink-model/docs/OrganismalEntity.html ?
    ?organismUri a bl:OrganismTaxon ;
      bl:id ?organismUri ;
      bl:name ?organismName .

    # bl:in_taxon could be interesting
    ?drugUri bl:affects ?organismUri .
  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      # Get primary DrugBank ID of drug
      ?drugObj x2rm:child_drugbank-id [ 
        x2rm:attribute_primary "true" ; 
        x2rm:hasValue ?drugbankId 
      ] .
      BIND(iri(concat("https://identifiers.org/drugbank:", ?drugbankId)) AS ?drugUri)
      ?drugObj x2rm:child_affected-organisms [ 
        x2rm:child_affected-organism ?organismObj
      ] .

      # Iterates on
      ?organismObj a x2rm:drugbank.drug.affected-organisms.affected-organism ;
        x2rm:hasValue ?organismName .


      BIND(iri(concat("https://w3id.org/d2s/data/organism:", SHA1(?organismName))) AS ?organismUri)

      # OPTIONAL{
      #   # It corresponds to the interacting drug name
      #   ?drugInteractionObj x2rm:child_name [
      #     x2rm:hasValue ?drugInteractionName
      #   ] .
      # }
    }
  }
}
