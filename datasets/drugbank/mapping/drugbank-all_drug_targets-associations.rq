PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/d2s/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT {
  GRAPH <?_output> {

    # Represent target (Drug to protein or DNA) ChemicalToGeneAssociation
    ?associationUri a bl:ChemicalToGeneAssociation ;
        bl:relation ?targetActionPropertyUri ;
        bl:subject ?drugUri ;
        bl:object ?bioentityUri ;
        bl:qualifiers ?actionUri ;
        bl:affects ?organismUri ;
        bl:relation ?targetActionPropertyUri .

    ?actionUri a bl:OntologyClass ;
      bl:id ?actionUri ;
      bl:name ?targetAction .

    ?bioentityUri a bl:GeneOrGeneProduct ;
        bl:name ?bioentityName ;
        bl:id ?bioentityUri ;
        bl:has_gene_product ?polypeptideUniprotUri .
        # We link directly the polypeptide with the drug it is affected by and the drugbank BioEntity

    ?organismUri a bl:OrganismTaxon ;
      bl:id ?organismUri ;
      bl:name ?bioentityOrganism .

    ?polypeptideAssociationUri a bl:ChemicalToGeneAssociation ;
        bl:relation ?targetActionPropertyUri ;
        bl:subject ?drugUri ;
        bl:object ?polypeptideUniprotUri ;
        bl:qualifiers ?actionUri ;
        bl:affects ?organismUri ;
        bl:relation ?targetActionPropertyUri .
  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {
      # Iterates on those different associations articles
      VALUES( ?bioentityType ?associationType ) {
        ("target" x2rm:drugbank.drug.targets.target )
        ("enzyme" x2rm:drugbank.drug.enzymes.enzyme )
        ("carrier" x2rm:drugbank.drug.carriers.carrier )
        ("transporter" x2rm:drugbank.drug.transporters.transporter )
        ("related_to" UNDEF )
      }
      # amino-acid-sequence or gene-sequence?
      # Not on the Drug articles, no way to represent it properly in BioLink
      # (x2rm:drugbank.drug.general-references.articles.article )

      # Get primary DrugBank ID of drug
      ?drugObj a x2rm:drugbank.drug ;
        x2rm:child_drugbank-id [ 
        x2rm:attribute_primary "true" ; 
        x2rm:hasValue ?drugbankId 
      ] .
      BIND(iri(concat("https://identifiers.org/drugbank:", ?drugbankId)) AS ?drugUri)

      # e.g. ?drugObj x2rm:child_TARGETS [ 
      #        x2rm:child_TARGET ?targetObj
      ?drugObj ?targetsPredicate [ 
        ?targetPredicate ?targetObj 
      ] .

      # Iterate on, and get target ID 
      # ?targetObj a x2rm:drugbank.drug.targets.target ;
      ?targetObj a ?associationType ;
       x2rm:child_id [ x2rm:hasValue ?bioentityId ] ;
       x2rm:child_polypeptide [
        x2rm:attribute_id ?polypeptideId ;
        # x2rm:attribute_source ?polypeptideSource ; # Swiss-Prot and Chembl, but the 2 are uniprot ID
      ]
.

      OPTIONAL{ ?targetObj x2rm:child_name [ x2rm:hasValue ?bioentityName ] . }
      OPTIONAL{ 
        ?targetObj x2rm:child_organism [ x2rm:hasValue ?bioentityOrganism ] . 
        BIND(iri(concat("https://w3id.org/d2s/data/organism:", SHA1(?bioentityOrganism))) AS ?organismUri )
      }
      # e.g. Human
      OPTIONAL{ ?targetObj x2rm:child_actions [ 
          x2rm:child_action [ x2rm:hasValue ?targetAction ] 
          # e.g. substrate, inhibitor
        ] . 

        # Define association relation depending on the action
        # TODO: substrate
        VALUES (?bioentityType ?targetAction ?targetActionPropertyUri) {
            ( "target" "inhibitor" bl:decreases_activity_of )
            ( "target" "antagonist" bl:decreases_activity_of )
            ( "target" "agonist" bl:increases_activity_of )
            ( "target" "activator" bl:increases_activity_of )
            ( "enzyme" "inhibitor" bl:decreases_activity_of )
            ( "enzyme" "antagonist" bl:decreases_activity_of )
            ( "enzyme" "agonist" bl:increases_activity_of )
            ( "enzyme" "activator" bl:increases_activity_of )
            ( "transporter" "inhibitor" bl:decreases_transport_of )
            ( "transporter" "antagonist" bl:decreases_transport_of )
            ( "transporter" "agonist" bl:increases_transport_of )
            ( "transporter" "activator" bl:increases_transport_of )
            ( "carrier" "inhibitor" bl:decreases_transport_of )
            ( "carrier" "antagonist" bl:decreases_transport_of )
            ( "carrier" "agonist" bl:increases_transport_of )
            ( "carrier" "activator" bl:increases_transport_of )
            ( "related_to" "inhibitor" bl:decreases_transport_of )
            ( "related_to" "antagonist" bl:decreases_transport_of )
            ( "related_to" "agonist" bl:increases_transport_of )
            ( "related_to" "activator" bl:increases_transport_of )
            ( "related_to" UNDEF bl:related_to )
            ( "target" UNDEF bl:affects_activity_of )
            ( "enzyme" UNDEF bl:affects_activity_of )
            ( "transporter" UNDEF bl:affects_transport_of )
            ( "carrier" UNDEF bl:affects_transport_of )
            ( UNDEF UNDEF bl:related_to )
        }
        BIND(iri(concat("https://w3id.org/d2s/data/drug_action:", SHA1(?targetAction))) AS ?actionUri )
      }
      # OPTIONAL{ ?targetObj x2rm:child_name [ x2rm:hasValue ?bioentityKnownAction ] . }
      # e.g. unknown, yes

      FILTER (?bioentityType != "")

      BIND(iri(concat("https://identifiers.org/drugbankv4.target:", ?bioentityId)) AS ?bioentityUri)
      BIND(iri(concat("https://w3id.org/d2s/data/association/drug_", ?bioentityType, ":", ?drugbankId, "_", ?bioentityId)) AS ?associationUri)

      # Define polypeptide association
      BIND(iri(concat("https://identifiers.org/uniprot:", ?polypeptideId)) AS ?polypeptideUniprotUri)
      BIND(iri(concat("https://w3id.org/d2s/data/association/drug_protein:", ?drugbankId, "_", ?polypeptideId)) AS ?polypeptideAssociationUri)
    }
  }
}
