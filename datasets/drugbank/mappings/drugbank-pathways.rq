PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/d2s/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT {
  GRAPH <?_output> {

    ?drugPathwayAssociationUri a bl:ChemicalToPathwayAssociation ;
      bl:relation bl:affects_activity_of ;
      bl:subject ?drugUri ;
      bl:object ?smpdbPathwayUri .

      # Direct link
      #?drugUri bl:affects_activity_of ?drugPathwayUri .

    ?smpdbPathwayUri a bl:Pathway ;
      bl:id ?smpdbId ;
      bl:name ?pathwayName .
  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      # Get primary DrugBank ID of drug
      ?drugObj x2rm:child_drugbank-id [ 
        x2rm:attribute_primary "true" ; 
        x2rm:hasValue ?drugbankId 
      ] .
      BIND(iri(concat("https://identifiers.org/drugbank:", ?drugbankId)) AS ?drugUri)
      ?drugObj x2rm:child_pathways [ 
        x2rm:child_pathway ?pathwayObj
      ] .

      # Iterate on
      ?pathwayObj a x2rm:drugbank.drug.pathways.pathway ;
        x2rm:child_smpdb-id [ x2rm:hasValue ?smpdbId ] .

      BIND( iri(concat("https://identifiers.org/smpdb:", ?smpdbId)) AS ?smpdbPathwayUri )
      BIND( iri(concat("https://w3id.org/d2s/data/association/chemical_pathway:", concat(?drugbankId, "_", ?smpdbId))) AS ?drugPathwayAssociationUri )

      OPTIONAL{
        ?pathwayObj x2rm:child_name [
          x2rm:hasValue ?pathwayName
        ] .
      }
    }
  }
}
