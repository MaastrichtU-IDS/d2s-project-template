PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/d2s/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT {
  GRAPH <?_output> {

    ### Represent Drug drug interaction. https://biolink.github.io/biolink-model/docs/ChemicalToThingAssociation.html
    # Or should we use https://biolink.github.io/biolink-model/docs/affects.html
    # Issue on BioLink GitHub: https://github.com/biolink/biolink-model/issues/253
    ?foodInteractionAssociationUri a bl:ChemicalToThingAssociation ;
      bl:id ?foodInteractionAssociationUri ;
      bl:relation bl:affects_activity_of ;
      bl:subject ?interactingFoodUri ;
      bl:object ?drugUri ;
      bl:description ?foodInteractionDescription .

    # Could not find better type for food
    ?interactingFoodUri a bl:Carbohydrate ;
      bl:id ?interactingFoodUri ;
      bl:name ?foodInteractionDescription .

    # ?drugUri a bl:Drug .
  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      # Get primary DrugBank ID of drug
      ?drugObj x2rm:child_drugbank-id [ 
        x2rm:attribute_primary "true" ; 
        x2rm:hasValue ?drugbankId 
      ] .
      BIND(iri(concat("https://identifiers.org/drugbank:", ?drugbankId)) AS ?drugUri)
      ?drugObj x2rm:child_food-interactions [ 
        x2rm:child_food-interaction ?foodInteractionObj
      ] .

      # Iterate on
      ?foodInteractionObj a x2rm:drugbank.drug.food-interactions.food-interaction .

      ?foodInteractionObj x2rm:hasValue ?foodInteractionDescription .

      BIND(SHA1(?foodInteractionDescription) AS ?foodInteractionId)
      BIND(iri(concat("https://w3id.org/d2s/data/foodinteraction:", ?foodInteractionId)) AS ?interactingFoodUri)
      BIND(iri(concat("https://w3id.org/d2s/data/association/drug_foodinteraction:", concat(?drugbankId, "_", ?foodInteractionId))) AS ?foodInteractionAssociationUri)

      # OPTIONAL{
      #   # It corresponds to the interacting drug name
      #   ?drugInteractionObj x2rm:child_name [
      #     x2rm:hasValue ?drugInteractionName
      #   ] .
      # }
    }
  }
}
