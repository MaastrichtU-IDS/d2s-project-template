PREFIX d2s: <https://w3id.org/d2s/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/d2s/vocab/>

## See issue: https://github.com/MaastrichtU-IDS/data2services-project/issues/15
INSERT {
  GRAPH <?_output> {  
   ?associationUri a bl:Association ;
      bl:id ?associationUri ;
      bl:subject ?uriConcept1 ;
      bl:object ?uriConcept2 ;
      bl:provided_by ?providerUri ;
      bl:has_confidence_level ?confidenceUri ;
      bl:frequency_qualifier ?frequencyUri .

    ?confidenceUri a bl:ConfidenceLevel ;
      bl:id ?confidenceUri ;
      bl:name ?confidenceName ;
      bl:has_count ?Concept_count ;
      bl:has_total ?patientCountInDataset ;
      bl:provided_by ?providerUri .


    ?chiFrequencyUri a bl:FrequencyValue ;
      bl:id ?chiFrequencyUri ;
      bl:name ?chiFrequencyName ;
      bl:has_quantitative_value ?chiValueUri ;
      bl:has_qualitative_value ?Chi_square_p ;
      # TODO: should be an URI to a bl:NamedThing
      bl:has_attribute_type <https://w3id.org/biolink/cohd/method/stats/chisquare> .

    ?ratioFrequencyUri a bl:FrequencyValue ;
      bl:id ?ratioFrequencyUri ;
      bl:name ?ratioFrequencyName ;
      bl:has_quantitative_value ?ratioValueUri ;
      bl:has_qualitative_value ?Expected_count ;
      # TODO: should be an URI to a bl:NamedThing
      bl:has_attribute_type <https://w3id.org/biolink/cohd/method/stats/lnratio> .

    ?chiValueUri a bl:QuantityValue ;
      bl:has_numeric_value ?Chi_square_t .
      # bl:has_unit ?unit .

    ?ratioValueUri a bl:QuantityValue ;
      bl:has_numeric_value ?Ln_ratio .
      # bl:has_unit ?unit .

  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      ?row a <https://w3id.org/d2s/paired_concept_counts_associations.tsv> ;
        d2s:Dataset_id ?Dataset_id ;
        d2s:Concept_id_1 ?Concept_id_1 ;
        d2s:Concept_id_2 ?Concept_id_2 ;
        d2s:Concept_count ?Concept_count ;
        d2s:Chi_square_t ?Chi_square_t ;
        d2s:Chi_square_p ?Chi_square_p ;
        d2s:Expected_count ?Expected_count ;
        d2s:Ln_ratio ?Ln_ratio .
        #d2s:Concept_prevalence ?Concept_prevalence ;
        #d2s:Rel_freq_1 ?Rel_freq_1 ;
        #d2s:Rel_freq_2 ?Rel_freq_2 .

      # Get dataset URI and count of patients in the dataset (for bl:has_total)
      ?datasetRow a <https://w3id.org/d2s/patient_count.tsv> ;
        d2s:Dataset_id ?Dataset_id ;
        d2s:Count ?patientCountInDataset .
      BIND ( iri(concat("https://w3id.org/biolink/cohd/provider/dataset/", ?Dataset_id)) AS ?providerUri )

      # Create URI for concepts
      BIND ( iri(concat("http://api.ohdsi.org/WebAPI/vocabulary/concept/", ?Concept_id_1)) AS ?uriConcept1 )
      BIND ( iri(concat("http://api.ohdsi.org/WebAPI/vocabulary/concept/", ?Concept_id_2)) AS ?uriConcept2 )

      # Create Provider URI from COHD dataset id
      BIND ( concat(?Dataset_id, "_", ?Concept_id_1, "_", ?Concept_id_2) as ?associationId)
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/", ?associationId)) AS ?associationUri )

      # Generate URI and label for ConfidenceLevel
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/confidence/", ?associationId)) AS ?confidenceUri )
      BIND ( concat("Count of co-occurences of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " with the total count of patient in the dataset") AS ?confidenceName )

      # Generate URI and label for chi square FrequencyValue
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/frequency/chisquare/", ?associationId)) AS ?chiFrequencyUri )
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/frequency/chisquare/", ?associationId, "/tvalue")) AS ?chiValueUri )
      BIND ( concat("Chi Square of occurences of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " in the population observed, with p-value") AS ?chiFrequencyName )

      # Generate URI and label for chi square FrequencyValue
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/frequency/ratio/", ?associationId)) AS ?ratioFrequencyUri )
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/frequency/ratio/", ?associationId, "/value")) AS ?ratioValueUri )
      BIND ( concat("Ln ratio of observation/expected of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " in the population observed (expected count provided)") AS ?ratioFrequencyName )
    }
  }
}
