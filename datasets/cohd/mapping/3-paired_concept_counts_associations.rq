PREFIX d2s: <https://w3id.org/d2s/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/d2s/vocab/>

## See issue: https://github.com/MaastrichtU-IDS/data2services-project/issues/15
INSERT {
  GRAPH <?_output> {  
    # Attribute the retrieved data to your model properties
   ?associationUri a bl:Association ;
      bl:id ?associationUri ;
      bl:subject ?uriConcept1 ;
      bl:object ?uriConcept2 ;
      bl:provided_by ?providerUri ;
      bl:has_confidence_level ?confidenceUri ;
      bl:frequency_qualifier ?frequencyUri .
      # bl:label ?Chi_square_p ;
      # bl:label ?Expected_count ;
      # bl:label ?Concept_prevalence ;
      # bl:label ?Ln_ratio ;
      # bl:label ?Rel_freq_1 ;
      # bl:label ?Rel_freq_2 .

    ?confidenceUri a bl:ConfidenceLevel ;
      bl:id ?confidenceUri ;
      bl:name ?confidenceName ;
      bl:has_count ?Concept_count ;
      bl:has_total ?patientCountInDataset ;
      bl:provided_by ?providerUri .

    ?frequencyUri a bl:FrequencyValue ;
      bl:id ?frequencyUri ;
      bl:name ?frequencyName ;
      bl:has_quantitative_value ?quantitativeValueUri .
      # bl:has_qualitative_value ?qualitativeValueUri ;
      # bl:has_attribute_type ?freqAttributeType ;

    ?quantitativeValueUri a bl:QuantitativeValue .
      # bl:has_numeric_value ?numericValue ;
      # bl:has_unit ?unit .


  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      ?row d2s:Dataset_id ?Dataset_id ;
        a <https://w3id.org/d2s/paired_concept_counts_associations.tsv> .

      # Get dataset URI and count of patients in the dataset (for bl:has_total)
      ?datasetRow a <https://w3id.org/d2s/patient_count.tsv> ;
        d2s:Dataset_id ?Dataset_id ;
        d2s:Count ?patientCountInDataset .
      BIND ( iri(concat("https://w3id.org/biolink/cohd/provider/dataset/", ?Dataset_id)) AS ?providerUri )

      ?row d2s:Concept_id_1 ?Concept_id_1 . 
      ?row d2s:Concept_id_2 ?Concept_id_2 .

      BIND ( iri(concat("http://api.ohdsi.org/WebAPI/vocabulary/concept/", ?Concept_id_1)) AS ?uriConcept1 )
      BIND ( iri(concat("http://api.ohdsi.org/WebAPI/vocabulary/concept/", ?Concept_id_2)) AS ?uriConcept2 )

      BIND ( concat(?Dataset_id, "_", ?Concept_id_1, "_", ?Concept_id_2) as ?associationId)
      # Generate URI from ID
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/", ?associationId)) AS ?associationUri )

      # Optional? Seems to not properly get the variables
      ?row d2s:Concept_count ?Concept_count . 
      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/confidence/", ?associationId)) AS ?confidenceUri )
      BIND ( concat("Count of co-occurences of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " with the total count of patient in the dataset") AS ?confidenceName )

      BIND ( iri(concat("https://w3id.org/biolink/cohd/association/frequency/", ?associationId)) AS ?frequencyUri )
      BIND ( concat("Frequency of occurences of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " in the population observed") AS ?frequencyName )

      # OPTIONAL { ?row d2s:Concept_prevalence ?Concept_prevalence . }
      # OPTIONAL { ?row d2s:Chi_square_t ?Chi_square_t . }
      # OPTIONAL { ?row d2s:Chi_square_p ?Chi_square_p . }
      # OPTIONAL { ?row d2s:Expected_count ?Expected_count . }
      # OPTIONAL { ?row d2s:Ln_ratio ?Ln_ratio . }
      # OPTIONAL { ?row d2s:Rel_freq_1 ?Rel_freq_1 . }
      # OPTIONAL { ?row d2s:Rel_freq_2 ?Rel_freq_2 . }
    }
  }
}
