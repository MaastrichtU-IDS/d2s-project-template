PREFIX d2s: <https://w3id.org/d2s/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/d2s/vocab/>

## See issue: https://github.com/MaastrichtU-IDS/data2services-project/issues/15
# Not executed at the moment because, not scalable on 27G TSV, using RMLStreamer
INSERT {
  GRAPH <?_output> {  
   ?associationUri a bl:Association ;
      bl:id ?associationUri ;
      bl:subject ?uriConcept1 ;
      bl:object ?uriConcept2 ;
      bl:relation bl:related_to ;
      bl:provided_by ?providerUri ;
      bl:has_evidence ?evidenceUri .
      # bl:has_confidence_level ?confidenceUri ;
      # bl:frequency_qualifier ?frequencyUri .

    # ?confidenceUri a bl:ConfidenceLevel ;
    #   bl:id ?confidenceUri ;
    #   bl:name ?confidenceName ;
    #   bl:has_count ?Concept_count ;
    #   bl:has_total ?patientCountInDataset ;
    #   bl:provided_by ?providerUri .
    # ?frequencyUri a bl:FrequencyValue ;
    #   bl:id ?frequencyUri ;
    #   bl:name ?chiFrequencyName ;
    #   # bl:has_quantitative_value ?chiValueUri ;
    #   bl:has_qualitative_value ?Chi_square_p ;
    #   # TODO: should be an URI to a bl:NamedThing
    #   bl:has_attribute_type <https://w3id.org/trek/cohd/method/stats/chisquare> .

    ?evidenceUri a bl:EvidenceType ;
      bl:id ?evidenceUri ;
      bl:name ?evidenceName ;
      bl:has_count ?Concept_count ;
      bl:has_total ?patientCountInDataset ;
      bl:has_quotient ?Prevalence ;
      bl:provided_by ?providerUri ;
      bl:has_attribute ?tTestChiUri ;
      bl:has_attribute ?pValueChiUri ; # Should we use bl:has_confidence_level ?
      bl:has_attribute ?relFreq1Uri ;
      bl:has_attribute ?relFreq2Uri ;
      bl:has_attribute ?expectedCountUri ;
      bl:has_attribute ?lnRatioUri .
      # TODO: Search this in an ontology (should be an URI to a bl:NamedThing)
      # bl:category? To define the type of evidence


    ?tTestChiUri a bl:Attribute ;
      bl:id ?tTestChiUri ;
      bl:name "t-Test results" ;
      bl:has_attribute_type <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C53231> ; # t-Test
      bl:has_quantitative_value ?tTestChiValue .
    ?tTestChiValue a bl:QuantityValue ;
      bl:has_numeric_value ?Chi_square_t .

    # NCIT: https://bioportal.bioontology.org/ontologies/NCIT?p=classes&conceptid=http%3A%2F%2Fncicb.nci.nih.gov%2Fxml%2Fowl%2FEVS%2FThesaurus.owl%23C53231


    # TODO: Use Confidence level?
    ?pValueChiUri a bl:Attribute ;
      bl:id ?pValueChiUri ;
      bl:name "t-Test p-value" ;
      bl:has_attribute_type <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C44185> ; # p-value
      bl:has_quantitative_value ?pValueChiValue .
    ?pValueChiValue a bl:QuantityValue ;
      bl:has_numeric_value ?Chi_square_p .


    ?relFreq1Uri a bl:Attribute ;
      bl:id ?relFreq1Uri ;
      bl:name "Relative frequence of the association subject (count of patients with the 2 concepts divided by count of patients with only one)" ;
      bl:has_attribute_type <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C118873> ; # How often / Frequency
      bl:has_quantitative_value ?relFreq1Value .
    ?relFreq1Value a bl:QuantityValue ;
      bl:has_numeric_value ?Rel_freq_1 .

    ?relFreq2Uri a bl:Attribute ;
      bl:id ?relFreq2Uri ;
      bl:name "Relative frequence of the association object (count of patients with the 2 concepts divided by count of patients with only one)" ;
      bl:has_attribute_type <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C118873> ;
      bl:has_quantitative_value ?relFreq2Value .
    ?relFreq2Value a bl:QuantityValue ;
      bl:has_numeric_value ?Rel_freq_2 .

    # https://bioportal.bioontology.org/ontologies/NCIT?p=classes&conceptid=http%3A%2F%2Fncicb.nci.nih.gov%2Fxml%2Fowl%2FEVS%2FThesaurus.owl%23C118873

    ?expectedCountUri a bl:Attribute ;
      bl:id ?expectedCountUri ;
      bl:name "Expected count given the 2 concepts are not associated" ;
      bl:has_attribute_type <http://semanticscience.org/resource/SIO_001021> ; # Expected Value.
      bl:has_quantitative_value ?expectedCountValue .
    ?expectedCountValue a bl:QuantityValue ;
      bl:has_numeric_value ?Expected_count .

      # NCIT: https://bioportal.bioontology.org/ontologies/NCIT?p=classes&conceptid=http%3A%2F%2Fncicb.nci.nih.gov%2Fxml%2Fowl%2FEVS%2FThesaurus.owl%23C25501
      # SIO: http://semanticscience.org/resource/SIO_001021


    ?lnRatioUri a bl:Attribute ;
      bl:id ?lnRatioUri ;
      bl:name "Log of observed patients with the concept on the expected count" ;
      bl:has_attribute_type <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C44256> ; # Ratio
      bl:has_quantitative_value ?lnRatioValue .
    ?lnRatioValue a bl:QuantityValue ;
      bl:has_numeric_value ?Ln_ratio .

      # NCIT: http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C44256


  }
} WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {

      ?row a <https://w3id.org/d2s/paired_concept_counts_associations.tsv> ;
        d2s:Dataset_id ?Dataset_id ;
        d2s:Concept_id_1 ?Concept_id_1 ;
        d2s:Concept_id_2 ?Concept_id_2 ;
        d2s:Concept_count ?Concept_count ;
        d2s:Chi_square_t ?Chi_square_t ;
        d2s:Chi_square_p ?Chi_square_p ;
        d2s:Expected_count ?Expected_count ;
        d2s:Ln_ratio ?Ln_ratio ;
        d2s:Concept_prevalence ?Concept_prevalence ;
        d2s:Rel_freq_1 ?Rel_freq_1 ;
        d2s:Rel_freq_2 ?Rel_freq_2 .

      # Get dataset URI and count of patients in the dataset (for bl:has_total)
      ?datasetRow a <https://w3id.org/d2s/patient_count.tsv> ;
        d2s:Dataset_id ?Dataset_id ;
        d2s:Count ?patientCountInDataset .
      BIND ( iri(concat("https://w3id.org/trek/cohd/provider/dataset/", ?Dataset_id)) AS ?providerUri )

      # Create URI for concepts
      BIND ( iri(concat("http://api.ohdsi.org/WebAPI/vocabulary/concept/", ?Concept_id_1)) AS ?uriConcept1 )
      BIND ( iri(concat("http://api.ohdsi.org/WebAPI/vocabulary/concept/", ?Concept_id_2)) AS ?uriConcept2 )

      # Create Provider URI from COHD dataset id
      BIND ( concat(?Dataset_id, "_", ?Concept_id_1, "_", ?Concept_id_2) as ?associationId)
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/", ?associationId)) AS ?associationUri )

      # Generate URI and label for the Evidence instance
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId)) AS ?evidenceUri )
      BIND ( concat("Count of co-occurences of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " with the total count of patient in the dataset") AS ?evidenceName )

      # Generate URIs of the Attributes and their values URIs
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/ttest")) AS ?tTestChiUri )
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/ttest/value")) AS ?tTestChiValue )

      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/pvalue")) AS ?pValueChiUri )
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/pvalue/value")) AS ?pValueChiValue )

      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/subject_relative_frequency")) AS ?relFreq1Uri )
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/subject_relative_frequency/value")) AS ?relFreq1Value )

      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/object_relative_frequency")) AS ?relFreq2Uri )
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/object_relative_frequency/value")) AS ?relFreq2Value )

      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/expected_count")) AS ?expectedCountUri )
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/expected_count/value")) AS ?expectedCountValue )

      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/ln_ratio")) AS ?lnRatioUri )
      BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId, "/ln_ratio/value")) AS ?lnRatioValue )



      # # # Generate URI and label for chi square FrequencyValue
      # # BIND ( iri(concat("https://w3id.org/trek/cohd/association/frequency/chisquare/", ?associationId)) AS ?frequencyUri )
      # # BIND ( iri(concat("https://w3id.org/trek/cohd/association/frequency/chisquare/", ?associationId, "/tvalue")) AS ?chiValueUri )
      # # BIND ( concat("Chi Square of occurences of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " in the population observed, with p-value") AS ?chiFrequencyName )

      # # BIND ( iri(concat("https://w3id.org/trek/cohd/association/evidence/", ?associationId)) AS ?evidenceUri )

      # # Generate URI and label for chi square FrequencyValue
      # BIND ( iri(concat("https://w3id.org/trek/cohd/association/frequency/ratio/", ?associationId)) AS ?ratioFrequencyUri )
      # BIND ( iri(concat("https://w3id.org/trek/cohd/association/frequency/ratio/", ?associationId, "/value")) AS ?ratioValueUri )
      # BIND ( concat("Ln ratio of observation/expected of the 2 concepts ", ?Concept_id_1, " ", ?Concept_id_2, " in the population observed (expected count provided)") AS ?ratioFrequencyName )
    }
  }
}
