PREFIX d2smodel: <https://w3id.org/d2s/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/d2s/vocab/>
# hgnc_id	symbol	name	locus_group	locus_type	status	location	location_sortable	alias_symbol	alias_name	prev_symbol	prev_name	gene_family	gene_family_id	date_approved_reserved	date_symbol_changed	date_name_changed	date_modified	entrez_id	ensembl_gene_id	vega_id	ucsc_id	ena	refseq_accession	ccds_id	uniprot_ids	pubmed_id	mgd_id	rgd_id	lsdb	cosmic	omim_id	mirbase	homeodb	snornabase	bioparadigms_slc	orphanet	pseudogene.org	horde_id	merops	imgt	iuphar	kznf_gene_catalog	mamit-trnadb	cd	lncrnadb	enzyme_id	intermediate_filament_db	rna_central_ids	lncipedia	gtrnadb	agr
# HGNC:5	A1BG	alpha-1-B glycoprotein	protein-coding gene	gene with protein product	Approved	19q13.43	19q13.43					Immunoglobulin like domain containing	594	1989-06-30			2015-07-13	1	ENSG00000121410	OTTHUMG00000183507	uc002qsd.5		NM_130786	CCDS12976	P04217	2591067	MGI:2152878	RGD:69417		A1BG	138670								I43.950												HGNC:5
# HGNC:37133	A1BG-AS1	A1BG antisense RNA 1	non-coding RNA	RNA, long non-coding	Approved	19q13.43	19q13.43	FLJ23569		"NCRNA00181|A1BGAS|A1BG-AS"	"non-protein coding RNA 181|A1BG antisense RNA (non-protein coding)|A1BG antisense RNA 1 (non-protein coding)"	Antisense RNAs	1987	2009-07-20	2010-11-25	2012-08-15	2013-06-27	503538	ENSG00000268895	OTTHUMG00000183508	uc002qse.3	BC040926	NR_015380																										A1BG-AS1		HGNC:37133
# HGNC:24086	A1CF	APOBEC1 complementation factor	protein-coding gene	gene with protein product	Approved	10q11.23	10q11.23	"ACF|ASP|ACF64|ACF65|APOBEC1CF"				RNA binding motif containing	725	2007-11-23			2016-10-05	29974	ENSG00000148584	OTTHUMG00000018240	uc057tgv.1	AF271790	NM_014576	"CCDS7241|CCDS7242|CCDS7243|CCDS73133"	Q9NQ94	"11815617|11072063"	MGI:1917115	RGD:619834		A1CF	618199																				HGNC:24086

INSERT
{
  GRAPH <?_output> {

    ## TODO: use ?geneSymbolUri as main URI for gene?

    ?hgncUri a bl:Gene ;
      bl:id ?hgncUriId ;
      bl:name ?Name ;
      bl:same_as ?geneSymbolUri ;
      bl:systematic_synonym ?geneSymbol ;
      bl:synonym ?singleAlias_name ;
      w3idvocab:status ?Status . # Use bl:ConfidenceLevel (Level of confidence in a statement)? Values: "Approved", "Symbol Withdrawn", "Entry Withdrawn"

    ?hgncUri bl:category ?Locus_type .
    # Could it be bl:category? Values: "RNA, long non-coding", "gene with protein product", "pseudogene", "virus integration site"
    ?hgncUri bl:category ?Locus_group .
    # Almost same as Locus_type. Values: "non-coding RNA", "protein-coding gene", "pseudogene", "other"

    #?hgncUri bl:GenomicSequenceLocalization ?
    # Should we use https://biolink.github.io/biolink-model/docs/located_in.html ?

    ?hgncUri bl:update_date ?DateModified .

    ?hgncUri bl:same_as ?EntrezIdUri ; # https://www.ncbi.nlm.nih.gov/gene/1047
      bl:same_as ?EnsemblGeneUri ; # https://identifiers.org/ensembl/ENSG00000153132
      bl:same_as ?OmimIdUri ; # https://identifiers.org/mim/601858
      bl:same_as ?AccessionNumbersUri ; # https://www.ncbi.nlm.nih.gov/nuccore/D86322
      bl:same_as ?EnsemblIdSuppliedByEnsemblUri .

    # https://biolink.github.io/biolink-model/docs/TranscriptToGeneRelationship.html
    ?refseqAssociationUri a bl:TranscriptToGeneRelationship ;
      bl:id ?refseqAssociationUriId ;
      bl:subject ?RefseqAccessionUri ;
      bl:object ?hgncUri ;
      bl:relation bl:expresses .

    ?RefseqAccessionUri a bl:Transcript ;
      bl:id ?RefseqAccessionUriId . # https://www.ncbi.nlm.nih.gov/nuccore/NM_004362

    # https://biolink.github.io/biolink-model/docs/has_gene_product.html
    ?hgncUri bl:has_gene_product ?UniprotIdsUri .

    ?UniprotIdsUri a bl:Protein ;
      bl:id ?UniprotIdsUriId .

    # https://biolink.github.io/biolink-model/docs/GeneToGeneHomologyAssociation.html
    ?mgiAssociationUri a bl:GeneToGeneHomologyAssociation ;
            bl:id ?mgiAssociationUriId ;
            bl:relation bl:homologous_to ;
            bl:gene_to_gene_association_subject ?hgncUri ;
            bl:gene_to_gene_association_object ?MouseGenomeDatabaseIdUri .

    ?suppliedMgiAssociationUri a bl:GeneToGeneHomologyAssociation ;
            bl:id ?suppliedMgiAssociationUriId ;
            bl:relation bl:homologous_to ;
            bl:gene_to_gene_association_subject ?hgncUri ;
            bl:gene_to_gene_association_object ?MouseGenomeDatabaseIdSuppliedByMgiUri .

    ?rgdAssociationUri a bl:GeneToGeneHomologyAssociation ;
            bl:id ?rgdAssociationUriId ;
            bl:relation bl:homologous_to ;
            bl:gene_to_gene_association_subject ?hgncUri ;
            bl:gene_to_gene_association_object ?RgdIdUri .


    ?hgncUri bl:located_in ?GenomicSequenceLocalizationUri ;
      bl:located_in ?CcdsIdUri . # TODO: atm it generates https://identifiers.org/ccds/CCDS7241 but this does not resolve. It would need to be https://identifiers.org/ccds/CCDS7241.1

    ?GenomicSequenceLocalizationUri a bl:GenomicSequenceLocalization ;
      bl:id ?GenomicSequenceLocalizationUriId ;
      bl:genomic_sequence_localization_subject ?hgncUri ;
      bl:genomic_sequence_localization_object ?LocationUri .

    ?LocationUri a bl:GenomicEntity ;
      bl:id ?LocationUriId ;
      bl:Name ?Location ;
      bl:category "chromosome" .


    # Publication
    ?hgncUri bl:publications ?PubmedIdUri .
    ?PubmedIdUri a bl:Publication ;
      bl:id ?Pubmed_id ;
      bl:iri ?PubmedIdUri ;
      bl:relates_to ?hgncUri .

    ?hgncUri bl:filler ?GeneFamilyUri .
    ?GeneFamilyUri a <https://w3id.org/d2s/model/GeneFamily> ;
      bl:id ?GeneFamilyUriId ;
      bl:subClassOf bl:Attribute ;
      bl:name ?Gene_family .

    # Use bl:ConfidenceLevel? (Level of confidence in a statement) Values: "Approved", "Symbol Withdrawn", "Entry Withdrawn"
    ?hgncUri bl:filler ?StatusUri .
    ?StatusUri a <https://w3id.org/d2s/model/Status> ;
          rdfs:subClassOf bl:Attribute ;
          bl:Name ?Status .

 }
}
WHERE {
  SERVICE <?_service>  {
    GRAPH <?_input> {
      ?s d2smodel:Hgnc_id ?Hgnc_id .
       OPTIONAL { 
         ?s d2smodel:Name ?Name .
       
       } OPTIONAL {
         ?s d2smodel:Symbol ?geneSymbol .
         BIND( iri(concat("https://identifiers.org/hgnc.symbol:", ?geneSymbol)) AS ?geneSymbolUri)
       
       } OPTIONAL { 
         ?s d2smodel:Status ?Status .
         BIND ( iri(concat("https://w3id.org/d2s/model/status/", REPLACE(?Status, " ", "", "i")) ) AS ?StatusUri )
       
       } OPTIONAL { 
         ?s d2smodel:Locus_type ?Locus_type .
       
       } OPTIONAL { 
         ?s d2smodel:Locus_group ?Locus_group .
       
       } OPTIONAL {
         ?s d2smodel:Alias_symbol ?Alias_symbol . # array
         BIND( if( contains(?Alias_symbol, "|"),
              strbefore(?Alias_symbol, "|") ,
              ?Alias_symbol )
         AS ?singleAlias_symbol )
       
       } OPTIONAL {
         ?s d2smodel:Alias_name ?Alias_name . # array
         BIND( if( contains(?Alias_name, "|"),
              strbefore(?Alias_name, "|") ,
              ?Alias_name )
         AS ?singleAlias_name )

       } OPTIONAL {
         ?s d2smodel:Prev_symbol ?Prev_symbol . # array
         BIND( if( contains(?Prev_symbol, "|"),
              strbefore(?Prev_symbol, "|") ,
              ?Prev_symbol )
         AS ?singlePrev_symbol )
       
       } OPTIONAL {
         ?s d2smodel:Prev_name ?Prev_name . # array

       } OPTIONAL {
         ?s d2smodel:Gene_family ?Gene_family . # array
         BIND( if( contains(?Gene_family, "|"),
              iri(concat("https://w3id.org/d2s/model/gene-family/", md5(strbefore(?Gene_family, "|")) ) ) ,
              iri(concat("https://w3id.org/d2s/model/gene-family/", md5(?Gene_family) ) ) ) 
         AS ?GeneFamilyUri ) # TODO: not good for URI
         BIND ( str(?GeneFamilyUri) AS ?GeneFamilyUriId )

       } OPTIONAL {
         ?s d2smodel:Gene_family_id ?Gene_family_id . # array
         # TODO: Generate URI fropm this one instead of using md5?!
       
       } OPTIONAL {
         ?s d2smodel:Synonyms ?Synonyms .
       
       } OPTIONAL {
         ?s d2smodel:NameSynonyms ?NameSynonyms .
       
       } OPTIONAL {
         # TODO: also do Location_sortable? WHY this if statement???
         ?s d2smodel:Location ?Location .
         BIND( if( contains(?Location, " "),
              iri("") ,
              iri(concat("https://w3id.org/d2s/model/sequence-localization/", md5(concat(?Hgnc_id, ?Location))))
              ) 
         AS ?GenomicSequenceLocalizationUri )
         BIND ( str(?GenomicSequenceLocalizationUri) AS ?GenomicSequenceLocalizationUriId )

         BIND( if( contains(?Location, " "),
              iri("") ,
              iri(concat("https://w3id.org/d2s/model/chromosome/", ?Location) )
              ) 
         AS ?LocationUri )
         BIND ( str(?LocationUri) AS ?LocationUriId )

         #FILTER NOT EXISTS { contains(?Location, " ")  }

         #BIND( iri(concat("https://w3id.org/d2s/model/sequence-localization/", md5(concat(?Hgnc_id, ?Location)))) AS ?GenomicSequenceLocalizationUri )

         #BIND( iri(concat("https://w3id.org/d2s/model/chromosome/", ?Location) ) AS ?LocationUri )
       
       } OPTIONAL {
         ?s d2smodel:Date_approved_reserved ?Date_approved_reserved . # Format: 2009-07-20

       } OPTIONAL {
         ?s d2smodel:Date_symbol_changed ?Date_symbol_changed . # Format: 2009-07-20

       } OPTIONAL {
         ?s d2smodel:Date_name_changed ?Date_name_changed . # Format: 2009-07-20

       } OPTIONAL {
         ?s d2smodel:Date_modified ?Date_modified . # Format: 2009-07-20
       
       } OPTIONAL {
         ?s d2smodel:Entrez_id ?Entrez_id .
         BIND( iri(concat("https://www.ncbi.nlm.nih.gov/gene/", ?Entrez_id) ) AS ?EntrezIdUri )

       } OPTIONAL {
         ?s d2smodel:Ensembl_gene_id ?Ensembl_gene_id .
         BIND( iri(concat("https://identifiers.org/ensembl:", ?Ensembl_gene_id) ) AS ?EnsemblGeneIdUri )

       } OPTIONAL {
         ?s d2smodel:Vega_id ?Vega_id .

       } OPTIONAL {
         ?s d2smodel:Ucsc_id ?Ucsc_id .

       } OPTIONAL { 
         ?s d2smodel:Ena ?Ena .
         BIND( if( contains(?Ena, "|"),
              strbefore(?Ena, "|") ,
              ?Ena )
         AS ?singleEna )

       } OPTIONAL {
         ?s d2smodel:Refseq_accession ?Refseq_accession .
         BIND( if( contains(?Refseq_accession, "|"),
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore:", strbefore(?Refseq_accession, "|") ) ) ,
              iri(concat("https://www.ncbi.nlm.nih.gov/nuccore:", ?Refseq_accession ) ) )
         AS ?RefseqAccessionUri )
         BIND ( str(?RefseqAccessionUri) AS ?RefseqAccessionUriId)
         BIND ( iri(concat("https://w3id.org/d2s/model/association/transcript/", md5(concat(?Hgnc_id, ?Refseq_accession)))) AS ?refseqAssociationUri )
         BIND ( str(?refseqAssociationUri) AS ?refseqAssociationUriId)

       } OPTIONAL {
         ?s d2smodel:Ccds_id ?Ccds_id .

         BIND( if( contains(?Ccds_id, "|"),
                iri(concat("https://identifiers.org/ccds:", strbefore(?Ccds_id, "|") ) ) ,
                iri(concat("https://identifiers.org/ccds:", ?Ccds_id ) ) )
         AS ?CcdsIdUri )

       } OPTIONAL {
         ?s d2smodel:Uniprot_ids ?Uniprot_ids .
         # If there is a comma we take only the first iteration.
         # TODO: also take the STRAFTER?
         BIND( if( contains(?Uniprot_ids, "|"),
              iri(concat("https://identifiers.org/uniprot:", strbefore(?Uniprot_ids, "|") ) ) ,
              iri(concat("https://identifiers.org/uniprot:", ?Uniprot_ids ) ) ) 
         AS ?UniprotIdsUri )
         BIND(str(?UniprotIdsUri) AS ?UniprotIdsUriId)


       } OPTIONAL {
         ?s d2smodel:Pubmed_id ?Pubmed_id .
         BIND( if( contains(?Pubmed_id, "|"),
              iri(concat("https://identifiers.org/pubmed:", strbefore(?Pubmed_id, "|") ) ) ,
              iri(concat("https://identifiers.org/pubmed:", ?Pubmed_id ) ) ) 
         AS ?PubmedIdUri )

       } OPTIONAL {
         ?s d2smodel:Mgd_id ?Mgd_id .
         BIND( if( contains(?Mgd_id, "|"),
              iri(concat("https://identifiers.org/", strbefore(?Mgd_id, "|") ) ) ,
              iri(concat("https://identifiers.org/", ?Mgd_id ) ) ) 
         AS ?MouseGenomeDatabaseIdUri )
         BIND ( iri(concat("https://w3id.org/d2s/model/association/homology/", md5(concat(?Hgnc_id, ?Mgd_id)))) AS ?mgiAssociationUri )
         BIND ( str(?mgiAssociationUri) AS ?mgiAssociationUriId )

       } OPTIONAL {
         ?s d2smodel:Rgd_id ?Rgd_id .
         BIND( if( contains(?Rgd_id, "|"),
              iri(concat("https://identifiers.org/", strbefore(?Rgd_id, "|") ) ) ,
              iri(concat("https://identifiers.org/", ?Rgd_id ) ) ) 
         AS ?RgdIdUri )
         BIND ( iri(concat("https://w3id.org/d2s/model/association/homology/", md5(concat(?Hgnc_id, ?Rgd_id)))) AS ?rgdAssociationUri )
         BIND ( str(?rgdAssociationUri) AS ?rgdAssociationUriId )

       } OPTIONAL {
         ?s d2smodel:Lsdb ?Lsdb .
         BIND( if( contains(?Lsdb, "|"),
              iri(concat("https://identifiers.org/", strafter(?Lsdb, "|") ) ) ,
              iri(concat("https://identifiers.org/", ?Lsdb ) ) ) 
         AS ?LsdbUri ) # TODO: LRG_359|http://ftp.ebi.ac.uk/pub/databases/lrgex/LRG_359.xml

       
       } OPTIONAL {
         ?s d2smodel:Cosmic ?Cosmic .

       } OPTIONAL {
         ?s d2smodel:Omim_id ?Omim_id .
         # TODO: also take the STRAFTER?
         BIND( if( contains(?Omim_id, "|"),
              iri(concat("https://identifiers.org/mim/", strbefore(?Omim_id, "|") ) ) ,
              iri(concat("https://identifiers.org/mim/", ?Omim_id ) ) ) 
         AS ?OmimIdUri )

       } OPTIONAL {
         ?s d2smodel:Mirbase ?Mirbase .
         # TODO: identifiers.org/mirbase:MI0000651
         BIND( iri(concat("https://identifiers.org/mirbase/", ?Mirbase ) ) 
         AS ?MirbaseUri )

       } OPTIONAL {
         ?s d2smodel:Homeodb ?Homeodb .
         # TODO: 8553

       } OPTIONAL {
         ?s d2smodel:Snornabase ?Snornabase .
         # Not really interesting DB https://www-snorna.biotoul.fr/plus.php?id=U8 (SR0000002)

       } OPTIONAL {
         ?s d2smodel:Bioparadigms_slc ?Bioparadigms_slc . # TODO: SLC49A1
       
       } OPTIONAL {
         ?s d2smodel:Orphanet ?Orphanet . # TODO: 246647

       } OPTIONAL {
         ?s d2smodel:PseudogeneOrg ?PseudogeneOrg . # TODO: PGOHUM00000291221

       } OPTIONAL {
         ?s d2smodel:Horde_id ?Horde_id . # TODO: OR1A1

       } OPTIONAL {
         ?s d2smodel:Merops ?Merops . # TODO: I43.950

       } OPTIONAL {
         ?s d2smodel:Imgt ?Imgt . # TODO: IGHA2

       } OPTIONAL {
         ?s d2smodel:Iuphar ?Iuphar . # TODO: objectId:763

       } OPTIONAL {
         ?s d2smodel:Kznf_gene_catalog ?Kznf_gene_catalog . # TODO: 1226

       } OPTIONAL {
         ?s d2smodel:MamitTrnadb ?MamitTrnadb . # TODO: 13

       } OPTIONAL {
         ?s d2smodel:Cd ?Cd . # TODO: CD243


       } OPTIONAL {
         ?s d2smodel:Lncrnadb ?Lncrnadb . # TODO: AFAP1AS

       } OPTIONAL {
         ?s d2smodel:Enzyme_id ?Enzyme_id . # array TODO: 6.2.1.16
         BIND( if( contains(?Enzyme_id, "|"),
              iri(concat("https://identifiers.org/ec-code/", strbefore(?Enzyme_id, "|") ) ) ,
              iri(concat("https://identifiers.org/ec-code/", ?Enzyme_id ) ) ) 
         AS ?EnzymeIdsUri )

       } OPTIONAL {
         ?s d2smodel:Intermediate_filament_db ?Intermediate_filament_db . # TODO: HGNC:1040

       } OPTIONAL {
         ?s d2smodel:Rna_central_ids ?Rna_central_ids . # TODO: URS00007E4F6E
         BIND( iri(concat("https://rnacentral.org/rna/", ?Rna_central_ids) ) AS ?RnaCentralIdsUri )
       }

  BIND ( iri(concat("https://identifiers.org/", ?Hgnc_id)) AS ?hgncUri )
  BIND ( str(?Hgnc_id) AS ?hgncUriId )

}
}
}