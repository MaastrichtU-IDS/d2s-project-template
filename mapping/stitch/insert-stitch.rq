PREFIX d2smodel: <https://w3id.org/data2services/model/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
PREFIX w3idvocab: <https://w3id.org/data2services/vocab/>
INSERT
{
  GRAPH <?_outputGraph> {  
    ?chemical1Uri a bl:ChemicalSubstance ;
      bl:id ?chemical1Curie .
    ?chemical2Uri a bl:ChemicalSubstance ;
      bl:id ?chemical2Curie .

    # Drug drug interaction
    ?associationUri a bl:ChemicalToThingAssociation ;
      bl:relation bl:affects_activity_of ;
      bl:subject ?chemical1Uri ;
      bl:object ?chemical2Uri ;
      bl:has_confidence_level ?ConfidenceLevelUri .
    
    ?ConfidenceLevelUri a bl:ConfidenceLevel ;
      bl:id "text mining score" ;
      bl:name "Text mining score" ;
      bl:node_property ?score .

 }
}
WHERE {
  SERVICE <?_serviceUrl>  {
    GRAPH <?_inputGraph> {
      # Transform https://stringdb-static.org/download/protein.actions.v10.5/9606.protein.actions.v10.5.txt.gz

      # CIDs is a stereo-specific compound, and the suffix is the PubChem compound id.
      # CIDm is a "flat" compound, i.e. with merged stereo-isomers. The suffix is the PubChem compound id.
      ?s d2smodel:Chemical1 ?chemical1 ; # e.g. CIDm00024759 or CIDs91758695
        d2smodel:Chemical2 ?chemical2 ;
        d2smodel:Textmining ?textminingScore . # integer score (e.g. 471)
      
      BIND ( iri(concat("https://w3id.org/data2services/data/stitch/interaction/", md5(concat(?chemical1, ?chemical2, ?textminingScore)) )) AS ?associationUri )
      BIND ( iri(replace(?chemical1, "CID[ms]", "https://identifiers.org/pubchem.compound:")) AS ?chemical1Uri )
      BIND ( iri(replace(?chemical2, "CID[ms]", "https://identifiers.org/pubchem.compound:")) AS ?chemical2Uri )
      BIND ( iri(replace(?chemical1, "CID[ms]", "pubchem.compound:")) AS ?chemical1Curie )
      BIND ( iri(replace(?chemical2, "CID[ms]", "pubchem.compound:")) AS ?chemical2Curie )

      # Avoid taking the first row with columns labels
      FILTER( ?chemical1 != "chemical1")
    }
  }
}