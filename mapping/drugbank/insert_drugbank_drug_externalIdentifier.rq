PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT 
{
    GRAPH <?_outputGraph> {  
        # external-identifiers
        ?drugUri ?drugIdentifierProperty ?drugIdentifierUri .
        ?drugIdentifierUri a bl:Drug ;
            bl:id ?drugIdentifierId .
    }
}
WHERE {
    SERVICE <?_serviceUrl>  {
        GRAPH <?_inputGraph> {

            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get EXTERNAL IDENTIFIERS
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.external-identifiers; 
                    x2rm:hasChild [ 
                        a x2rm:drugbank.drug.external-identifiers.external-identifier;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.external-identifiers.external-identifier.resource; 
                            x2rm:hasValue ?drugIdentifierResource 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.external-identifiers.external-identifier.identifier; 
                            x2rm:hasValue ?drugIdentifierId 
                        ]
                    ]
                ] .

            # Match external-identifier with identifiers.org namespaces identifiers.org. And check if works
            VALUES (?drugIdentifierResource ?drugIdentifierNamespace) {
                ( "KEGG Compound" "kegg.compound" )
                ( "KEGG Drug" "kegg.drug" )
                ( "ChEMBL" "chembl.compound" )
                ( "ChEBI" "chebi/CHEBI:" )
                ( "PubChem Compound" "pubchem.compound" )
                ( "PubChem Substance" "pubchem.substance" )
                ( "PharmGKB" "pharmgkb.drug" )
                ( "GenAtlas" "genatlas" )
                ( "Wikipedia" "wikipedia.en" )
                ( "Guide to Pharmacology" "iuphar.ligand" )
                ( "IUPHAR" "iuphar.ligand" )
                ( "ChemSpider" "chemspider" )
                ( "Therapeutic Targets Database" "ttd.drug" )
                ( "Human Metabolome Database" "hmdb" )

                #( "Drugs Product Database (DPD)" "cdpd" )
                #( "GenBank Gene Database" "ena.embl" )
                #( "GenBank Protein Database" "ncbiprotein" )
                #( "HUGO Gene Nomenclature Committee (HGNC)" "hgnc" )

                #( "UniProtKB" "uniprot" )
                #( "GenBank" "" ) # https://www.ncbi.nlm.nih.gov/nuccore/M32315
                #( "BindingDB" "bindingDB" )
                #( "PDB" "pdb" )
            }

            VALUES (?drugIdentifierResource ?drugIdentifierProperty) {
                ( "KEGG Compound" bl:same_as ) # ok
                ( "KEGG Drug" bl:same_as ) # ok
                ( "ChEMBL" bl:same_as ) # ok
                ( "ChEBI" bl:same_as ) # ok
                ( "PubChem Compound" bl:same_as ) # ok
                ( "PubChem Substance" bl:same_as ) # ok
                ( "PharmGKB" bl:same_as ) # ok
                ( "GenAtlas" bl:same_as ) # ok
                ( "Wikipedia" bl:same_as ) # ok
                ( "Guide to Pharmacology" bl:same_as ) # ok
                ( "IUPHAR" bl:same_as ) # ok
                ( "ChemSpider" bl:same_as ) #ok
                ( "Therapeutic Targets Database" bl:same_as ) # ok
                ( "Human Metabolome Database" bl:same_as ) # ok
                ( undef undef )

                #( "Drugs Product Database (DPD)" bl:same_as ) # identifiers.org outdated for cdpd. not resolving
                
                # Nothing generated
                #( "GenBank Gene Database" bl:same_as )
                #( "GenBank Protein Database" bl:same_as )
                #( "HUGO Gene Nomenclature Committee (HGNC)" bl:same_as ) # nothing generated
            
                # Not processed:
                # ( "UniProtKB" iri("") ) # External link to UniProt is not exactly a same_as, even if it points to a protein similar to the one in the drug.
                # How to represent that?
                # GenBank not a drug: https://www.ncbi.nlm.nih.gov/nuccore/M32315
                # Drugs.com https://www.drugs.com/cdi/denileukin-diftitox.html
                # RxList. Page not found https://www.rxlist.com/script/main/notfoundstatic.asp?refurl=/cgi/generic2/denileukin.htm
                # Human Metabolome Database
                # BindingDB: http://www.bindingdb.org/bind/chemsearch/marvin/MolStructure.jsp?monomerid=50205291
                # PDB: http://identifiers.org/pdb/SAM or PHE, CHT...

            }

            # "UniProt Accession" = ide or FCG3B_HUMAN

            BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("http://identifiers.org/", ?drugIdentifierNamespace, "/", ?drugIdentifierId) ) AS ?drugIdentifierUri )
        }
    }
}