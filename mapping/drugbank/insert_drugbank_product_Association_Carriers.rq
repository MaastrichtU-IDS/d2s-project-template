PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT { 
    GRAPH <?_outputGraph> {   

        ### Represent carrier (Drug to protein or DNA) ChemicalToGeneAssociation
        #Should we use: ?drugUri bl:interacts_with ?carrierUri .
        ?carrierAssociationUri a bl:ChemicalToGeneAssociation ;
            bl:relation ?carrierActionPropertyUri ;
            bl:subject ?drugUri ;
            bl:object ?carrierUri ;
            bl:publications ?carrierArticleUri ;
            bl:qualifiers ?carrierActionUri ;
            bl:affects ?carrierOrganism .

        # Direct link
        #?drugUri ?carrierActionPropertyUri ?carrierUri .

        ?carrierActionUri a bl:Attribute ;
            bl:name ?carrierAction .

        ?carrierUri a bl:GeneOrGeneProduct ;
            bl:name ?carrierName ;
            bl:id ?carrierId .

        # Publications
        ?carrierArticleUri a bl:Publication ;
            bl:id ?carrierArticlePubmedId ;
            bl:name ?carrierArticleCitation ;
            bl:relates_to ?carrierUri . 

    }
}
WHERE {
    SERVICE <?_serviceUrl>  {
        GRAPH <?_inputGraph> {

            # Get drug primary ID
            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get drug-carrierS associations
                x2rm:hasChild [
                    a x2rm:drugbank.drug.carriers; 
                    x2rm:hasChild [
                        a x2rm:drugbank.drug.carriers.carrier ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.carriers.carrier.id ; 
                            x2rm:hasValue ?carrierId
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.carriers.carrier.name; 
                            x2rm:hasValue ?carrierName 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.carriers.carrier.organism;
                            x2rm:hasValue ?carrierOrganism 
                        ] ;
                        # Possible ACTIONS:
                        # inhibitor, activator, agonist, antagonist, binder, antibody
                        x2rm:hasChild [
                            a x2rm:drugbank.drug.carriers.carrier.actions; 
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.carriers.carrier.actions.action; 
                                x2rm:hasValue ?carrierAction
                            ] 
                        ] ;
                        # Get drug-carriers PUBLICATIONS.
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.carriers.carrier.references;
                            x2rm:hasChild [
                                a x2rm:drugbank.drug.carriers.carrier.references.articles; 
                                x2rm:hasChild [ 
                                    a x2rm:drugbank.drug.carriers.carrier.references.articles.article;
                                    x2rm:hasChild [ 
                                        a x2rm:drugbank.drug.carriers.carrier.references.articles.article.pubmed-id; 
                                        x2rm:hasValue ?carrierArticlePubmedId 
                                    ];
                                    x2rm:hasChild [
                                        a x2rm:drugbank.drug.carriers.carrier.references.articles.article.citation; 
                                        x2rm:hasValue ?carrierArticleCitation
                                    ]
                                ] 
                            ] 
                        ]
                    ]
                ] .


            BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/drugbank/", ?carrierId)) AS ?carrierUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/association/carrier/", md5(concat(?drugId, ?carrierId)))) AS ?carrierAssociationUri )
            BIND ( iri(concat("https://w3id.org/data2services/vocab/action/", ?carrierAction)) AS ?carrierActionUri )

            BIND ( iri(concat("http://identifiers.org/pubmed/", ?carrierArticlePubmedId)) AS ?carrierArticleUri ) .

            VALUES (?carrierAction ?carrierActionPropertyUri) {
                ( "inhibitor" bl:decreases_transport_of )
                ( "antagonist" bl:decreases_transport_of )
                ( "agonist" bl:increases_transport_of )
                ( "activator" bl:increases_transport_of )
                (UNDEF bl:affects_transport_of)
            }
        }
    }
}