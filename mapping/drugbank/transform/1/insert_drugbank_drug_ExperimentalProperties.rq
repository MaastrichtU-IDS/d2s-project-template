PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT 
{ 
    GRAPH <?_output> {  
    ### experimental-properties
    ?drugUri bl:filler ?drugExpePropertiesIdUri .

    ?drugExpePropertiesIdUri a ?drugExpePropertiesType ;
        d2s:kind ?drugExpePropertiesKind ;
        d2s:value ?drugExpePropertiesValue ;
        d2s:source ?drugExpePropertiesSource .
    }

    ?drugExpePropertiesType rdfs:subClassOf bl:Attribute .
}
WHERE {
    SERVICE <?_service>  {
        GRAPH <?_input> {
            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                        x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                        x2rm:hasValue ?drugId
                ] ;
                ### Get EXPERIMENTAL PROPERTIES
                x2rm:hasChild [ a x2rm:drugbank.drug.experimental-properties; 
                    x2rm:hasChild [ a x2rm:drugbank.drug.experimental-properties.property;
                        x2rm:hasChild [ a x2rm:drugbank.drug.experimental-properties.property.kind; x2rm:hasValue ?drugExpePropertiesKind ] ;
                        x2rm:hasChild [ a x2rm:drugbank.drug.experimental-properties.property.value; x2rm:hasValue ?drugExpePropertiesValue ] ;
                        x2rm:hasChild [ a x2rm:drugbank.drug.experimental-properties.property.source; x2rm:hasValue ?drugExpePropertiesSource ]
                ] ] .

             BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
             BIND ( iri(concat("https://w3id.org/data2services/model/experimental-properties/", md5(concat(?drugUri, ?drugExpePropertiesKind))) ) AS ?drugExpePropertiesIdUri )
             BIND ( iri(concat("https://w3id.org/data2services/model/experimental-properties/", REPLACE(?drugExpePropertiesKind, " ", "", "i")) ) AS ?drugExpePropertiesType )
             # Generate type like: https://w3id.org/data2services/model/experimental-properties/MeltingPoint

        }
    }
}