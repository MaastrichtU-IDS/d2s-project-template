PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT { 
    GRAPH <?_outputGraph> {   

        ### Represent enzyme (Drug to protein or DNA) ChemicalToGeneAssociation
        #Should we use: ?drugUri bl:interacts_with ?enzymeUri .
        ?enzymeAssociationUri a bl:ChemicalToGeneAssociation ;
            bl:relation ?enzymeActionPropertyUri ;
            bl:subject ?drugUri ;
            bl:object ?enzymeUri ;
            bl:publications ?enzymeArticleUri ;
            bl:qualifiers ?enzymeActionUri ;
            bl:affects ?enzymeOrganism .


        # Direct link
        #?drugUri ?enzymeActionPropertyUri ?enzymeUri .

        ?enzymeActionUri a bl:Attribute ;
            bl:name ?enzymeAction .

        ?enzymeUri a bl:GeneOrGeneProduct ;
            bl:name ?enzymeName ;
            bl:id ?enzymeId .

        # Publications
        ?enzymeArticleUri a bl:Publication ;
            bl:id ?enzymeArticlePubmedId ;
            bl:name ?enzymeArticleCitation ;
            bl:relates_to ?enzymeUri . 

    }
}
WHERE {
    SERVICE <?_serviceUrl>  {
        GRAPH <?_inputGraph> {

            # Get drug primary ID
            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get drug-enzymeS associations
                x2rm:hasChild [
                    a x2rm:drugbank.drug.enzymes; 
                    x2rm:hasChild [
                        a x2rm:drugbank.drug.enzymes.enzyme ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.enzymes.enzyme.id ; 
                            x2rm:hasValue ?enzymeId
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.enzymes.enzyme.name; 
                            x2rm:hasValue ?enzymeName 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.enzymes.enzyme.organism;
                            x2rm:hasValue ?enzymeOrganism 
                        ] ;
                        # Possible ACTIONS:
                        # inhibitor, activator, agonist, antagonist, binder, antibody
                        x2rm:hasChild [
                            a x2rm:drugbank.drug.enzymes.enzyme.actions; 
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.enzymes.enzyme.actions.action; 
                                x2rm:hasValue ?enzymeAction
                            ] 
                        ] ;
                        # Get drug-enzymes PUBLICATIONS.
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.enzymes.enzyme.references;
                            x2rm:hasChild [
                                a x2rm:drugbank.drug.enzymes.enzyme.references.articles; 
                                x2rm:hasChild [ 
                                    a x2rm:drugbank.drug.enzymes.enzyme.references.articles.article;
                                    x2rm:hasChild [ 
                                        a x2rm:drugbank.drug.enzymes.enzyme.references.articles.article.pubmed-id; 
                                        x2rm:hasValue ?enzymeArticlePubmedId 
                                    ];
                                    x2rm:hasChild [
                                        a x2rm:drugbank.drug.enzymes.enzyme.references.articles.article.citation; 
                                        x2rm:hasValue ?enzymeArticleCitation
                                    ]
                                ] 
                            ] 
                        ]
                    ]
                ] .


            BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/drugbank/", ?enzymeId)) AS ?enzymeUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/association/enzyme/", md5(concat(?drugId, ?enzymeId)))) AS ?enzymeAssociationUri )
            BIND ( iri(concat("https://w3id.org/data2services/vocab/action/", ?enzymeAction)) AS ?enzymeActionUri )

            BIND ( iri(concat("http://identifiers.org/pubmed/", ?enzymeArticlePubmedId)) AS ?enzymeArticleUri ) .

            VALUES (?enzymeAction ?enzymeActionPropertyUri) {
                ( "inhibitor" bl:decreases_activity_of )
                ( "antagonist" bl:decreases_activity_of )
                ( "agonist" bl:increases_activity_of )
                ( "activator" bl:increases_activity_of )
                (UNDEF bl:affects)
            }
        }
    }
}