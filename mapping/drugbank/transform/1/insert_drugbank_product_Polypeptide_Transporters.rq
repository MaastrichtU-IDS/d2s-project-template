PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT { 
    GRAPH <?_output> {   

        ?transporterUri a bl:GeneOrGeneProduct ;
            bl:same_as ?polypeptideAttrUri ;
            bl:in_taxon ?ncbiTaxonIdentifierUri ;
            bl:category ?polypeptidePfamUri .

        # Get GO TERM
        ?goTermAssociationUri a bl:GeneToGoTermAssociation ;
            bl:relation bl:category ;
            bl:gene_to_go_term_association_subject ?transporterUri ;
            bl:gene_to_go_term_association_object ?goTermUri .

        # Direct link
        #?transporterUri bl:category ?goTermUri .

        ?goTermUri a bl:GeneOntologyClass ;
            bl:description ?polyGoTermDescription ;
            bl:category ?goTermCategoryUri .

        ?goTermCategoryUri a bl:OntologyClass ;
            bl:name ?polyGoTermCategory .


        # Get Pfam
        ?polypeptidePfamUri bl:id ?polyPfamId ;
            bl:name ?polyPfamName . 

        # Get TAXON
        ?ncbiTaxonIdentifierUri a bl:OrganismTaxon ;
            bl:id ?ncbiTaxonId ;
            bl:name ?ncbiTaxonName ;
            bl:same_as ?ncbiTaxonUri .

        ?polypeptideAttrUri a bl:GeneProduct .

    }
}
WHERE {
    SERVICE <?_service>  {
        GRAPH <?_input> {

            # Iterate directly on transporters
            ?transporterObj a x2rm:drugbank.drug.transporters.transporter ;
                # Get transporter ID
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.transporters.transporter.id ; 
                    x2rm:hasValue ?transporterId
                ] ;
                x2rm:hasChild [ 
                    a x2rm:drugbank.drug.transporters.transporter.polypeptide ;
                    x2rm:hasAttribute [
                        a x2rm:drugbank.drug.transporters.transporter.polypeptide.-id ;
                        x2rm:hasValue ?polypeptideAttrId
                    ] ;
                    x2rm:hasAttribute [
                        a x2rm:drugbank.drug.transporters.transporter.polypeptide.-source ;
                        x2rm:hasValue "Swiss-Prot"
                    ] ;
                    x2rm:hasChild [ 
                        a x2rm:drugbank.drug.transporters.transporter.polypeptide.pfams ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.transporters.transporter.polypeptide.pfams.pfam ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.transporters.transporter.polypeptide.pfams.pfam.identifier ;
                                x2rm:hasValue ?polyPfamId
                            ] ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.transporters.transporter.polypeptide.pfams.pfam.name ;
                                x2rm:hasValue ?polyPfamName
                            ]
                        ]
                    ] ;
                    x2rm:hasChild [ 
                        a x2rm:drugbank.drug.transporters.transporter.polypeptide.go-classifiers ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.transporters.transporter.polypeptide.go-classifiers.go-classifier ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.transporters.transporter.polypeptide.go-classifiers.go-classifier.category ;
                                x2rm:hasValue ?polyGoTermCategory
                            ] ;
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.transporters.transporter.polypeptide.go-classifiers.go-classifier.description ;
                                x2rm:hasValue ?polyGoTermDescription
                            ]
                        ]
                    ] ;
                    x2rm:hasChild [ 
                        a x2rm:drugbank.drug.transporters.transporter.polypeptide.organism ;
                        x2rm:hasAttribute [ 
                            a x2rm:drugbank.drug.transporters.transporter.polypeptide.organism.-ncbi-taxonomy-id ;
                            x2rm:hasValue ?ncbiTaxonId
                        ] ;
                        x2rm:hasValue ?ncbiTaxonName
                    ]
                ]

            BIND ( iri(concat("https://w3id.org/data2services/data/drugbank/", ?transporterId)) AS ?transporterUri )
            BIND ( iri(concat("http://identifiers.org/uniprot/", ?polypeptideAttrId)) AS ?polypeptideAttrUri )
            #BIND ( iri(concat("https://purl.uniprot.org/uniprot/", ?polypeptideAttrId)) AS ?polypeptideAttrUniprotUri )

            BIND ( iri(concat("https://w3id.org/data2services/data/association/go-term/", md5(concat(?transporterId, ?polyGoTermDescription)))) AS ?goTermAssociationUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/go-classifier/", md5(?polyGoTermDescription))) AS ?goTermUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/go-category/", ?polyGoTermCategory)) AS ?goTermCategoryUri )
            BIND ( iri(concat("http://identifiers.org/pfam/", ?polyPfamId)) AS ?polypeptidePfamUri )

            BIND ( iri(concat("http://identifiers.org/taxonomy/", ?ncbiTaxonId)) AS ?ncbiTaxonIdentifierUri )
            BIND ( iri(concat("https://www.ncbi.nlm.nih.gov/taxonomy/", ?ncbiTaxonId)) AS ?ncbiTaxonUri )

        }
    }
}