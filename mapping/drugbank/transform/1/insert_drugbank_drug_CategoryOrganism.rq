PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT { 
    GRAPH <?_output>
    { 
        # Affected organism
        ?drugUri bl:affects ?affectedOrganismUri .
        ?affectedOrganismUri a bl:OrganismalEntity .
        ?affectedOrganismUri bl:name ?affectedOrganism .

        # TODO: category. Is using this predicate right? https://biolink.github.io/biolink-model/docs/category.html
        ?drugUri bl:category ?categoryMeshUri .
        ?categoryMeshUri bl:id ?categoryMeshId .
        ?categoryMeshUri bl:name ?categoryName .
    }
}
WHERE {
    SERVICE <?_service>  {
        GRAPH  <?_input> { 

            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                        x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                        x2rm:hasValue ?drugId
                ] ;
                ### Get CATEGORIES (Mesh)
                x2rm:hasChild [ a x2rm:drugbank.drug.categories;
                    x2rm:hasChild [ a x2rm:drugbank.drug.categories.category;  
                        x2rm:hasChild [ a x2rm:drugbank.drug.categories.category.mesh-id; x2rm:hasValue ?categoryMeshId ] ;
                        x2rm:hasChild [ a x2rm:drugbank.drug.categories.category.category; x2rm:hasValue ?categoryName ] 
                ] ] .
            
            BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("http://identifiers.org/mesh/", ?categoryMeshId)) AS ?categoryMeshUri )
            # Get AFFECTED ORGANISM
            ?drugObj x2rm:hasChild [ a x2rm:drugbank.drug.affected-organisms; 
                x2rm:hasChild [ a x2rm:drugbank.drug.affected-organisms.affected-organism;  x2rm:hasValue ?affectedOrganism ] ] .
            BIND ( iri(concat("https://w3id.org/data2services/vocab/organism/", md5(?affectedOrganism))) AS ?affectedOrganismUri )

        }
    }
}