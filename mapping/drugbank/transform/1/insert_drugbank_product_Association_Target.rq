PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT { 
    GRAPH <?_output> {   

        ### Represent target (Drug to protein or DNA) ChemicalToGeneAssociation
        #Should we use: ?drugUri bl:interacts_with ?targetUri .
        ?targetAssociationUri a bl:ChemicalToGeneAssociation ;
            bl:relation ?targetActionPropertyUri ;
            bl:subject ?drugUri ;
            bl:object ?targetUri ;
            bl:publications ?targetArticleUri ;
            bl:qualifiers ?targetActionUri ;
            bl:affects ?targetOrganism ;
            bl:relation ?targetActionPropertyUri .

        # Direct link
        #?drugUri ?targetActionPropertyUri ?targetUri .

        ?targetActionUri a bl:Attribute ;
            bl:name ?targetAction .

        ?targetUri a bl:GeneOrGeneProduct ;
            bl:name ?targetName ;
            bl:id ?targetId .

        # Publications
        ?targetArticleUri a bl:Publication ;
            bl:id ?targetArticlePubmedId ;
            bl:name ?targetArticleCitation ;
            bl:relates_to ?targetUri . 

    }
}
WHERE {
    SERVICE <?_service>  {
        GRAPH <?_input> {

            # Get drug primary ID
            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get drug-TARGETS associations
                x2rm:hasChild [
                    a x2rm:drugbank.drug.targets; 
                    x2rm:hasChild [
                        a x2rm:drugbank.drug.targets.target ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.targets.target.id ; 
                            x2rm:hasValue ?targetId
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.targets.target.name; 
                            x2rm:hasValue ?targetName 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.targets.target.organism;
                            x2rm:hasValue ?targetOrganism 
                        ] ;
                        # Possible ACTIONS:
                        # inhibitor, activator, agonist, antagonist, binder, antibody
                        x2rm:hasChild [
                            a x2rm:drugbank.drug.targets.target.actions; 
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.targets.target.actions.action; 
                                x2rm:hasValue ?targetAction
                            ] 
                        ] ;
                        # Get drug-targets PUBLICATIONS.
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.targets.target.references;
                            x2rm:hasChild [
                                a x2rm:drugbank.drug.targets.target.references.articles; 
                                x2rm:hasChild [ 
                                    a x2rm:drugbank.drug.targets.target.references.articles.article;
                                    x2rm:hasChild [ 
                                        a x2rm:drugbank.drug.targets.target.references.articles.article.pubmed-id; 
                                        x2rm:hasValue ?targetArticlePubmedId 
                                    ];
                                    x2rm:hasChild [
                                        a x2rm:drugbank.drug.targets.target.references.articles.article.citation; 
                                        x2rm:hasValue ?targetArticleCitation
                                    ]
                                ] 
                            ] 
                        ]
                    ]
                ] .


            BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/drugbank/", ?targetId)) AS ?targetUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/association/target/", md5(concat(?drugId, ?targetId)))) AS ?targetAssociationUri )
            BIND ( iri(concat("https://w3id.org/data2services/vocab/action/", ?targetAction)) AS ?targetActionUri )

            BIND ( iri(concat("http://identifiers.org/pubmed/", ?targetArticlePubmedId)) AS ?targetArticleUri ) .

            VALUES (?targetAction ?targetActionPropertyUri) {
                ( "inhibitor" bl:decreases_activity_of )
                ( "antagonist" bl:decreases_activity_of )
                ( "agonist" bl:increases_activity_of )
                ( "activator" bl:increases_activity_of )
                (UNDEF bl:affects)
            }
        }
    }
}