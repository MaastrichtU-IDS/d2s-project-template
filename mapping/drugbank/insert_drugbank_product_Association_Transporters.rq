PREFIX x2rm: <http://ids.unimaas.nl/xml2rdf/model#>
PREFIX x2rd: <http://ids.unimaas.nl/xml2rdf/data/>
PREFIX d2s: <https://w3id.org/data2services/vocab/>
PREFIX ido: <http://identifiers.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>
INSERT { 
    GRAPH <?_outputGraph> {   

        ### Represent transporter (Drug to protein or DNA) ChemicalToGeneAssociation
        #Should we use: ?drugUri bl:interacts_with ?transporterUri .
        ?transporterAssociationUri a bl:ChemicalToGeneAssociation ;
            bl:relation ?transporterActionPropertyUri ;
            bl:subject ?drugUri ;
            bl:object ?transporterUri ;
            bl:publications ?transporterArticleUri ;
            bl:qualifiers ?transporterActionUri ;
            bl:affects ?transporterOrganism .

        # Direct link
        #?drugUri ?transporterActionPropertyUri ?transporterUri .

        ?transporterActionUri a bl:Attribute ;
            bl:name ?transporterAction .

        ?transporterUri a bl:GeneOrGeneProduct ;
            bl:name ?transporterName ;
            bl:id ?transporterId .

        # Publications
        ?transporterArticleUri a bl:Publication ;
            bl:id ?transporterArticlePubmedId ;
            bl:name ?transporterArticleCitation ;
            bl:relates_to ?transporterUri . 

    }
}
WHERE {
    SERVICE <?_serviceUrl>  {
        GRAPH <?_inputGraph> {

            # Get drug primary ID
            ?drugObj a x2rm:drugbank.drug ;
                # Get drug primary ID
                x2rm:hasChild [
                    a x2rm:drugbank.drug.drugbank-id ;
                    x2rm:hasAttribute [ a x2rm:drugbank.drug.drugbank-id.-primary ] ;
                    x2rm:hasValue ?drugId
                ] ;
                # Get drug-transporters associations
                x2rm:hasChild [
                    a x2rm:drugbank.drug.transporters; 
                    x2rm:hasChild [
                        a x2rm:drugbank.drug.transporters.transporter ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.transporters.transporter.id ; 
                            x2rm:hasValue ?transporterId
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.transporters.transporter.name; 
                            x2rm:hasValue ?transporterName 
                        ] ;
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.transporters.transporter.organism;
                            x2rm:hasValue ?transporterOrganism 
                        ] ;
                        # Possible ACTIONS:
                        # inhibitor, activator, agonist, antagonist, binder, antibody
                        x2rm:hasChild [
                            a x2rm:drugbank.drug.transporters.transporter.actions; 
                            x2rm:hasChild [ 
                                a x2rm:drugbank.drug.transporters.transporter.actions.action; 
                                x2rm:hasValue ?transporterAction
                            ] 
                        ] ;
                        # Get drug-transporters PUBLICATIONS.
                        x2rm:hasChild [ 
                            a x2rm:drugbank.drug.transporters.transporter.references;
                            x2rm:hasChild [
                                a x2rm:drugbank.drug.transporters.transporter.references.articles; 
                                x2rm:hasChild [ 
                                    a x2rm:drugbank.drug.transporters.transporter.references.articles.article;
                                    x2rm:hasChild [ 
                                        a x2rm:drugbank.drug.transporters.transporter.references.articles.article.pubmed-id; 
                                        x2rm:hasValue ?transporterArticlePubmedId 
                                    ];
                                    x2rm:hasChild [
                                        a x2rm:drugbank.drug.transporters.transporter.references.articles.article.citation; 
                                        x2rm:hasValue ?transporterArticleCitation
                                    ]
                                ] 
                            ] 
                        ]
                    ]
                ] .


            BIND ( iri(concat("http://identifiers.org/drugbank:", ?drugId)) AS ?drugUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/drugbank/", ?transporterId)) AS ?transporterUri )
            BIND ( iri(concat("https://w3id.org/data2services/data/association/transporter/", md5(concat(?drugId, ?transporterId)))) AS ?transporterAssociationUri )
            BIND ( iri(concat("https://w3id.org/data2services/vocab/action/", ?transporterAction)) AS ?transporterActionUri )

            BIND ( iri(concat("http://identifiers.org/pubmed/", ?transporterArticlePubmedId)) AS ?transporterArticleUri ) .

            VALUES (?transporterAction ?transporterActionPropertyUri) {
                ( "inhibitor" bl:decreases_transport_of )
                ( "antagonist" bl:decreases_transport_of )
                ( "agonist" bl:increases_transport_of )
                ( "activator" bl:increases_transport_of )
                (UNDEF bl:affects_transport_of)
            }

        }
    }
}